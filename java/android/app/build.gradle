apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url "https://oss.sonatype.org/content/repositories/comgoogle-1057/"
        }
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.4.2'
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "io.grpc.helloworldexample"
        minSdkVersion 7
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

sourceSets {
    main {
        proto {
            // Configure built-in outputs. Default is 'java'. Here we only
            // build javanano.
            builtins {
                remove java
                javanano {
                    // Options added to --javanano_out
                    option 'java_multiple_files=true'
                    option 'ignore_services=true'
                }
            }
            // Apply the 'grpc' plugin
            plugins {
                grpc {
                    // Options added to --grpc_out
                    option 'nano=true'
                }
            }
        }
    }
}

// Optional - define native codegen plugins pulled from repositories
//  Each entry is a '<name>:<plugin-groupId>:<plugin-artifactId>:<version>'.
//  '<plugin-groupId>:<plugin-artifactId>:<version>' is resolved and downloaded
//  from the repositories. Then this entry is transformed into a
//  'protobufCodeGenPlugins' entry '<name>:<path-to-downloaded-plugin>'.
// NOTE: we are still in the process of deploying the GRPC Java codegen to
// Maven Central. Currently there are no read-to-use codegen artifacts.
protobufNativeCodeGenPluginDeps = ["grpc:io.grpc:protoc-gen-grpc-java:0.1.0-SNAPSHOT"]

dependencies {
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'com.squareup.okhttp:okhttp:2.2.0'
    compile 'com.google.guava:guava:18.0'

    // You need to build the https://github.com/grpc/grpc-java
    // to obtain these libraries below.
    compile 'io.grpc:grpc-core:0.1.0-SNAPSHOT'
    compile 'io.grpc:grpc-protobuf-nano:0.1.0-SNAPSHOT'
    compile 'io.grpc:grpc-okhttp:0.1.0-SNAPSHOT'
    compile 'io.grpc:grpc-stub:0.1.0-SNAPSHOT'
}
